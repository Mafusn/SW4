options {
  STATIC = false;
}

PARSER_BEGIN(Compiler)

import AST.*;
import AST.CodeGeneration.CodeGenerator;
import AST.Nodes.*;
import AST.SymbolTableFilling.SymbolTableFilling;
import java.util.ArrayList;

public class Compiler {
    public static void main(String[] args) {
        try {
            Compiler compiler = new Compiler(new java.io.FileReader("Compiler/code.txt"));
            Node prog = compiler.Prog();

            PrettyPrint prettyPrint = new PrettyPrint();
            ArrayList<SymbolTableFilling> symbolTableFillings = new ArrayList<>();
            SymbolTableFilling symbolTableFilling = new SymbolTableFilling(symbolTableFillings , 0);
            TypeChecking typeChecking = new TypeChecking(symbolTableFillings);
            ConstantFolding constantFolding = new ConstantFolding();
            CodeGenerator codeGenerator = new CodeGenerator(symbolTableFillings);

            prog.accept(prettyPrint);
            System.out.println(prettyPrint.getResult());
            System.out.println();
            System.out.print("Syntax is good\n");
            prog.accept(symbolTableFilling);
            System.out.println("Symbol table is good");
            prog.accept(typeChecking);
            System.out.println("Type check is good");
            prog.accept(constantFolding);
            prog.accept(codeGenerator);
            System.out.println("Code generator is good");

            codeGenerator.generateCode();

        } catch (Throwable e) {
            // Catching Throwable is ugly but JavaCC throws Error objects!
            System.out.println("Syntax error: " + e.getMessage());
        }
    }
}

PARSER_END(Compiler)

SKIP: /* SKIP */
{ " "
| "\t"
| "\n"
| "\r"
| <COMMENT : "/*" (~["/"] | "/" ~["/"])* "*/" >
}

TOKEN : /* NUMBERS AND ARITHMETIC OPERATIONS */
{ <INTDCL: "int">
| <INT: ( < DIGIT > )>
| <FLOATDCL: "float">
| <FLOAT: ( < DIGIT > ) (".") ( < DIGIT > )+ >
| <#DIGIT: ( "0" | ["1"-"9"] ( ["0"-"9"] )* ) >
| <PLUS: "+">
| <MINUS: "-">
| <MULTIPLY: "*">
}

TOKEN: /* LOGIC */
{ <IF: "if">
| <ELSE: "else">
| <AND: "&&">
| <OR: "||">
| <NOT: "!">
| <EQ: "==">
| <NE: "!=">
| <LT: "<">
| <LE: "<=">
| <GT: ">">
| <GE: ">=">
| <BOOLDCL: "boolean">
| <TRUE: "true" >
| <FALSE: "false" >
}

TOKEN : /* ASSIGNMENT OPERATION AND BASIC CONTROL STRUCTURE */
{ <ASSIGN: "=">
| <LPAREN: "(">
| <RPAREN: ")">
| <LBRACE: "{">
| <RBRACE: "}">
| <END_OF_LINE: ";">
| <ADDRESS: "&">
| <HASHTAG: "#">
| <END_OF_FILE: "END">
| <WHILE: "while">
}

TOKEN : /* ID */
{ <ID: (["a"-"z", "A"-"Z", "_"]) (["a"-"z", "A"-"Z", "_", "0"-"9"])*> }


Node Prog() :
{
 Prog prog = new Prog();
 Node stmt = null;
 }
{( stmt = Stmt() { prog.addChild(stmt); } )* < END_OF_FILE > < END_OF_LINE >
{
    return prog;
}
}

Node Stmt() :
{
    Node ifStmt;
    Node dcl;
    Node expr;
    Node whileLoop;
    Token t;
    Token compAssOp = null;
}
{ t = < ID > (compAssOp = <PLUS> | compAssOp = <MINUS>)? < ASSIGN > expr = Expr() < END_OF_LINE >
{
    Id id = new Id(t.image);
    if (compAssOp != null) {
        return new AssignmentOp(t.image, id, expr, compAssOp.image);
    } else {
        return new AssignmentOp(t.image, id, expr);
    }
}
| dcl = Dcl() < END_OF_LINE >
{
    return dcl;
}
| ifStmt = IfStmt()
{
    return ifStmt;
}
| whileLoop = WhileLoop()
{
    return whileLoop;
}
}

Node Dcl() :
{
 boolean hasExpr = false;
 Node expr = null;
 Token t;
 }
{ < FLOATDCL > t = < ID > ( < ASSIGN > expr = Expr() { hasExpr = true; } )?
{
   if (hasExpr) {
       FloatDcl floatDcl = new FloatDcl(t.image);
       return new AssignmentOp(t.image, floatDcl, expr);
   } else {
       return new FloatDcl(t.image);
   }
}
| < INTDCL > t = < ID > ( < ASSIGN > expr = Expr() { hasExpr = true; } )?
{
    if (hasExpr) {
        IntDcl intDcl = new IntDcl(t.image);
        return new AssignmentOp(t.image, intDcl, expr);
    } else {
        return new IntDcl(t.image);
    }
}
| < BOOLDCL > t = < ID > ( < ASSIGN > expr = Expr() { hasExpr = true; } )?
{
    if (hasExpr) {
        BoolDcl boolDcl = new BoolDcl(t.image);
        return new AssignmentOp(t.image, boolDcl, expr);
    } else {
        return new BoolDcl(t.image);
    }
}
}

Node IfStmt() :
{
 Node expr;
 Node ifBlock;
 Node elseBlock = null;
 boolean withElse = false;
}
{
< IF > < LPAREN > expr = Expr() < RPAREN > < LBRACE > ifBlock = Block() < RBRACE > ( < ELSE > < LBRACE > elseBlock = Block() < RBRACE > { withElse = true; })?
{
    if (withElse) {
        return new IfElseStmt(expr, ifBlock, elseBlock);
    } else {
        return new IfStmt(expr, ifBlock);
    }
}
}

Node WhileLoop() :
{
    Node expr;
    Node block;
}
{ < WHILE > < LPAREN > expr = Expr() < RPAREN > < LBRACE > block = Block() < RBRACE >
{
    return new WhileLoop(expr, block);
}
}

Node Block() :
{
   Block block = new Block();
   Node stmt = null;
}
{
( stmt = Stmt() { block.addChild(stmt); } )*
{
    return block;
}
}

Node Expr() :
{
 Node expr;
}
{ expr = OrOp()
{
    return expr;
}
}

Node OrOp() :
{
    Node left = null;
    Token op = null;
    Node right = null;
}
{ left = AndOp() (( op = <OR> ) right = AndOp() { left = new ComparisonOp(left, op.image, right); })*
{
    return left;
}
}

Node AndOp() :
{
    Node left = null;
    Token op = null;
    Node right = null;
}
{ left = EqualityOp() (( op = <AND>) right = EqualityOp() { left = new ComparisonOp(left, op.image, right); })*
{
    return left;
}
}

Node EqualityOp() :
{
    Node left = null;
    Token op = null;
    Node right = null;
}
{ left = ComparisonOp() (( op = <EQ> | op = <NE>) right = ComparisonOp() { left = new ComparisonOp(left, op.image, right); })*
{
    return left;
}
}

Node ComparisonOp() :
{
    Node left = null;
    Token op = null;
    Node right = null;
 }
{left = ArithmeticOp() (( op = <LT> | op = <GT> | op = <GE> | op = <LE> ) right = ArithmeticOp() { left = new ComparisonOp(left, op.image, right); })*
{
    return left;
}
}

Node ArithmeticOp() :
{
    Node left = null;
    Token op = null;
    Node right = null;
 }
{ left = NegationOp() ((op = <PLUS> | op = <MINUS> ) right = NegationOp() { left = new ArithmeticOp(left, op.image, right); })*
{
    return left;
}

}
Node NegationOp() :
{
    Node factor;
    boolean hasNegationOp = false;
}
{
 ( <NOT> { hasNegationOp = true; })? factor = Factor()
{
    if (hasNegationOp) {
        return new NegationOp(factor);
    } else {
        return factor;
    }
}
}

Node Factor() :
{
    Node val;
    Node expr;
 }
{ <LPAREN> expr = Expr() <RPAREN>
{
    return expr;
}
| val = Val()
{
    return val;
}
}

Node Val() :
{
    Token t;
 }
{ t = < INT >
{
    return new IntNum(Integer.parseInt(t.image));
}
| t = < FLOAT >
{
    return new FloatNum(Float.parseFloat(t.image));
}
| t = < ID >
{
    return new Id(t.image);
}
| (t = < TRUE > | t = < FALSE >)
{
    return new Bool(Boolean.parseBoolean(t.image));
}
/*
| < ADDRESS > t = < ID >
{
    return new Bool(t.image);
}

 */
}

